plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id "com.jfrog.bintray" version "1.7.3"
}

group = "${project.group}.${project.name}"
version = "[v${project.modVersion}]-[MC${project.minecraftVersion}]-[F${project.forgeBuild}]-[S${project.spongeAPIVersion.split("-")[0]}]"


apply plugin: 'java'

sourceCompatibility = 1.8

project.tasks.assemble.dependsOn project.tasks.shadowJar

// Disable the 'jar' task
jar.enabled = false

repositories {
    mavenCentral()
}

def bintrayVersion = getVersion()+(System.getenv('TRAVIS') ? "."+System.getenv('TRAVIS_BUILD_NUMBER') : ".0")
def bintrayProjName = rootProject.name

setVersion(getVersion()+(System.getenv('TRAVIS') ? "."+System.getenv('TRAVIS_BUILD_NUMBER') : ".0")+(System.getenv('TRAVIS_TAG') ? "" : (System.getenv('TRAVIS') ? "-"+System.getenv('TRAVIS_COMMIT').take(7) : "")+"-SNAPSHOT"))

static def getSecret(String secretname){
    def result = System.getenv(secretname)
    if (result != null && result != "") return result;
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props[secretname]
}

ext.spongeApiVersion = "${project.spongeAPIVersion}"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    shadow "org.spongepowered:spongeapi:$spongeApiVersion"

    compile 'com.flowpowered:flow-nbt:1.0.0'
    compile group: 'com.mortennobel', name: 'java-image-scaling', version: '0.8.6'
    compile group: 'com.jhlabs', name: 'filters', version: '2.0.235-1'
}

shadowJar {
    classifier = null
    relocate 'com.flowpowered.nbt', 'com.vcsajen.yourcustompaintings.com.flowpowered.flow-nbt'
    relocate 'com.mortennobel.imagescaling', 'com.vcsajen.yourcustompaintings.com.mortennobel.imagescaling'
    relocate 'com.jhlabs', 'com.vcsajen.yourcustompaintings.com.jhlabs'
}

bintray {
    user = getSecret('BINTRAY_USER_NAME')
    key = getSecret('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'generic'
        name = 'YourCustomPaintings'
        publish = true
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/SparkVGX/YourCustomPaintings.git'
        version {
            name = bintrayVersion.toString()
            desc = bintrayProjName.toString() + bintrayVersion.toString()
            released = new Date()
            vcsTag = System.getenv('TRAVIS_TAG') ? System.getenv('TRAVIS_TAG') : System.getenv('TRAVIS_COMMIT')
        }
    }
}