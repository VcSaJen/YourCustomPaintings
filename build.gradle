plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id "org.spongepowered.ore" version "0.9.0"
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.qixalite.spongestart2' version '3.2.3'
    id 'signing'
}

group 'com.vcsajen'
version '1.2.0'

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8

project.tasks.assemble.dependsOn project.tasks.shadowJar

// Disable the 'jar' task
jar.enabled = false

repositories {
    mavenCentral()
}

def bintrayVersion = getVersion()+(System.getenv('TRAVIS') ? "."+System.getenv('TRAVIS_BUILD_NUMBER') : ".0")
def bintrayProjName = rootProject.name
setVersion(getVersion()+(System.getenv('TRAVIS') ? "."+System.getenv('TRAVIS_BUILD_NUMBER') : ".0")+(System.getenv('TRAVIS_TAG') ? "" : (System.getenv('TRAVIS') ? "-"+System.getenv('TRAVIS_COMMIT').take(7) : "")+"-SNAPSHOT"))
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

static def getSecret(String secretname){
    def result = System.getenv(secretname)
    if (result != null && result != "") return result
    def Properties props = new Properties()
    def File secretFile = new File('secrets.properties')
    if (!secretFile.exists()) return null
    props.load(new FileInputStream(secretFile))
    return props[secretname]
}

static String encodeHtmlEntities(String s) {
    s.collectReplacements {
        Character c ->
            int codePoint = c.toString().codePointAt(0)
            if (codePoint>0x007F) {
                '&#x'+Integer.toHexString(codePoint)+';'
            } else null
    }
}

def encHtmlEntitiesReplacement =

tasks.withType(Sign) {
    onlyIf { getSecret('CERT_KEY_ID')!=null }
}

signing {
    required { gradle.taskGraph.hasTask("oreDeploy") }
    sign configurations.shadow
}

allprojects {
    ext."signing.keyId" = getSecret('CERT_KEY_ID') ?: ""
    ext."signing.secretKeyRingFile" = "./secring.gpg"
    ext."signing.password" = getSecret('CERT_PWD') ?: ""
}

oreDeploy {
    apiKey = getSecret('ORE_API_KEY') ?: '00000000000000000000000000000000'
    recommended = isReleaseVersion
    deploy = configurations.archives
    channel = isReleaseVersion ? /*'release'*/'snapshot' : 'snapshot' //TODO: Fix when ORE fix their bug
    forumPost = isReleaseVersion
    changelog = encodeHtmlEntities(isReleaseVersion ? 'See intermediate snapshot versions for changelog' :
            "In the version "+getVersion()+", following changes were made:\r\n\r\n"+ //Useless padding to satisfy Ore's obsession with "min 15 characters"
            (System.getenv('TRAVIS_COMMIT_MESSAGE') ? System.getenv('TRAVIS_COMMIT_MESSAGE') : 'Unknown жツ'))
}

task signArchives(dependsOn: 'signShadow') {
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    shadow 'org.spongepowered:spongeapi:7.1.0'

    compile 'com.flowpowered:flow-nbt:1.0.0'
    compile group: 'com.mortennobel', name: 'java-image-scaling', version: '0.8.6'
    compile group: 'com.jhlabs', name: 'filters', version: '2.0.235-1'
}

shadowJar {
    classifier = null
    relocate 'com.flowpowered.nbt', 'com.vcsajen.yourcustompaintings.com.flowpowered.flow-nbt'
    relocate 'com.mortennobel.imagescaling', 'com.vcsajen.yourcustompaintings.com.mortennobel.imagescaling'
    relocate 'com.jhlabs', 'com.vcsajen.yourcustompaintings.com.jhlabs'
}

bintray {
    user = getSecret('BINTRAY_USER_NAME') ?: "user"
    key = getSecret('BINTRAY_API_KEY') ?: "0000000000000000000000000000000000000000"
    configurations = ['archives']
    pkg {
        repo = 'generic'
        name = 'YourCustomPaintings'
        publish = true
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/VcSaJen/YourCustomPaintings.git'
        version {
            name = bintrayVersion.toString()
            desc = bintrayProjName.toString() + bintrayVersion.toString()
            released = new Date()
            vcsTag = System.getenv('TRAVIS_TAG') ? System.getenv('TRAVIS_TAG') : System.getenv('TRAVIS_COMMIT')
        }
    }
}

spongestart {
    minecraft '1.12.2'
    forge '14.23.4.2705'
}
